if(TARGET osgQt::@LIB_NAME@)
    set(_OSGQOPENGL_ALREADY_DEFINED ON)
endif()

include(CMakeFindDependencyMacro)

find_dependency(Qt5 COMPONENTS @_QT_COMPONENTS@)
# Instead of checking OPENSCENEGRAPH_LIBRARIES, check osgViewer and osgUtil specifically
if(NOT DEFINED OSGUTIL_LIBRARIES OR NOT DEFINED OSGVIEWER_LIBRARIES OR NOT DEFINED OPENSCENEGRAPH_INCLUDE_DIRS)
    find_dependency(OpenSceneGraph COMPONENTS osgUtil osgViewer)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/@LIB_NAME@Targets.cmake")

if(NOT _OSGQOPENGL_ALREADY_DEFINED)
    # Replace the interface link libraries with those found by OSG, to allow this to be more easily relocatable.
    # Without this, absolute paths to OSG are stored in the target, making the output library non-relocatable
    # to other machines unless they have OSG in the exact same location.
    set_target_properties(osgQt::@LIB_NAME@ PROPERTIES INTERFACE_LINK_LIBRARIES "@TARGET_LIBRARIES@")
    target_link_libraries(osgQt::@LIB_NAME@ INTERFACE ${OPENSCENEGRAPH_LIBRARIES})
    target_include_directories(osgQt::@LIB_NAME@ INTERFACE ${OPENSCENEGRAPH_INCLUDE_DIRS})
endif()

# Add an alias as needed
if(NOT TARGET osgQt::osgQOpenGL)
    if(TARGET osgQt::osgQOpenGL-qt@QT_VERSION_MAJOR@)
        add_library(osgQt::osgQOpenGL ALIAS osgQt::osgQOpenGL-qt@QT_VERSION_MAJOR@)
    endif()
endif()
